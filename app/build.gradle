apply plugin: 'com.android.application'

android {
  compileSdkVersion 29

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  defaultConfig {
    applicationId 'tn.covid19.client'
    buildConfigField 'boolean', 'HIDDEN_APP', 'false'
    minSdkVersion 21
    targetSdkVersion 29
    versionCode 62
    versionName '6.1'
  }

  lintOptions {
    checkReleaseBuilds false
  }

  flavorDimensions 'default'
  productFlavors {
    regular {
      isDefault.set(true)
    }
    google
    hidden {
      applicationId 'tn.covid19.client.hidden'
      buildConfigField 'boolean', 'HIDDEN_APP', 'true'
    }
  }

  testOptions {
    unitTests {
      includeAndroidResources = true
    }
  }
}

dependencies {
  implementation 'com.google.android.material:material:1.2.0-alpha04'
  implementation 'androidx.preference:preference:1.1.0'
  implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
  implementation 'androidx.vectordrawable:vectordrawable:1.0.1'
  implementation 'androidx.navigation:navigation-fragment:2.2.1'
  implementation 'androidx.navigation:navigation-ui:2.2.1'
  implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
  implementation 'androidx.legacy:legacy-support-v4:1.0.0'
  implementation 'androidx.appcompat:appcompat:1.1.0'
  testImplementation 'junit:junit:4.13'
  testImplementation 'org.robolectric:robolectric:4.1'
  googleImplementation 'com.google.firebase:firebase-core:17.2.2'
  googleImplementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
  googleImplementation 'com.google.android.gms:play-services-location:17.0.0'

  // Jitsi Meet
  implementation('org.jitsi.react:jitsi-meet-sdk:2.5.1') { transitive = true }
}

if (getGradle().getStartParameter().getTaskRequests().toString().contains('Google')) {
  apply plugin: 'com.google.gms.google-services'
  apply plugin: 'io.fabric'

  task copyJson(type: Copy) {
    from '../../traccar-shared/google-services'
    into '.'
    include 'traccar-client-android.json'
    rename('traccar-client-android.json', 'google-services.json')
  }

  afterEvaluate {
    preBuild.dependsOn copyJson
  }
}
